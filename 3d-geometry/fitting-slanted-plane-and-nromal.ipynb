{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "430267e8-9da1-4264-a7a4-076fbeb254d1",
   "metadata": {},
   "source": [
    "# Fitting slanted plane for disparity estimation"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "5bc53eb7-197e-46d8-971c-181ab478cc10",
   "metadata": {},
   "source": [
    "> See HITNet paper (cvpr 2021) at https://arxiv.org/pdf/2007.12140.pdf and the [TensorFlow code](https://github.com/google-research/google-research/tree/master/hitnet) (only model, no source code) and [3rd party PyTorch code](https://github.com/zjjMaiMai/TinyHITNet). "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e35b2952-b514-4111-b576-bc7da20acddb",
   "metadata": {},
   "source": [
    "## Problem: Fitting a slanted disparity plane\n",
    "\n",
    "> During propagation we impose a loss on the tile geometry $d$, $d_x$, $d_y$ and the tile confidence $w$. We use the ground truth disparity $d^{\\text{gt}}$ and ground truth disparity gradients $d_x^{\\text{gt}}$\n",
    "and $d_y^{\\text{gt}}$, which we compute by robustly fitting a plane to $d^{\\text{gt}}$ in a $9 \\times 9$ window centered at the pixel."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "0117e54a-735d-4853-9414-4d1ab2c9566a",
   "metadata": {},
   "source": [
    "## How to do it?"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "036306d0-28ad-44a6-aaf7-9de34b144a9c",
   "metadata": {},
   "source": [
    "<center>\n",
    "<img src=\"./data/slanted-plane-01.jpg\" alt=\"4 columns of GL_MODELVIEW matrix \" width=\"600\"/>\n",
    "</center>\n",
    "\n",
    "<center>\n",
    "<img src=\"./data/slanted-plane-02.jpg\" alt=\"4 columns of GL_MODELVIEW matrix \" width=\"600\"/>\n",
    "</center>\n",
    "\n",
    "<center>\n",
    "<img src=\"./data/slanted-plane-03.jpg\" alt=\"4 columns of GL_MODELVIEW matrix \" width=\"600\"/>\n",
    "</center>"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a76bcfa3-6350-4267-b9da-a7bbcf526b02",
   "metadata": {},
   "source": [
    "## The CUDA Code"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "66211ba7-b550-4b1d-9a96-d9479144e355",
   "metadata": {},
   "source": [
    "```cpp\n",
    "#include <torch/extension.h>\n",
    "#include <cuda.h>\n",
    "#include <cuda_runtime.h>\n",
    "#include <c10/cuda/CUDAGuard.h>\n",
    "\n",
    "/*\n",
    "dx * x + dy * y = z(a, b) - z(0, 0);\n",
    "*/\n",
    "__global__ void plane_fitting_cuda_foward_kernel(\n",
    "    float sigma, // e.g., == 0.1;\n",
    "    float min_disp,\n",
    "    float max_disp,\n",
    "    torch::PackedTensorAccessor32<float, 4, torch::RestrictPtrTraits> input,\n",
    "    torch::PackedTensorAccessor32<int, 4, torch::RestrictPtrTraits> random,\n",
    "    torch::PackedTensorAccessor32<float, 3, torch::RestrictPtrTraits> output)\n",
    "    {\n",
    "    \n",
    "    const int N = input.size(0);\n",
    "    const int kernel_H = input.size(1); // e.g., kernel_H = 9\n",
    "    const int kernel_W = input.size(2); // e.g., kernel_W = 9\n",
    "    const int L = input.size(3);\n",
    "    const int I = random.size(2); // iter, e.g., I=256\n",
    "\n",
    "    const int Index = blockIdx.x * blockDim.x + threadIdx.x;\n",
    "    if (Index >= N * L)\n",
    "    {\n",
    "        return;\n",
    "    }\n",
    "    const int n = Index / L;\n",
    "    const int l = Index % L;\n",
    "\n",
    "    int max_inlier = 0;\n",
    "    float best_dx = 0.0f;\n",
    "    float best_dy = 0.0f;\n",
    "    \n",
    "    // E.g., the center pixel is (9//2, 9//2)=(4,4);\n",
    "    float z00 = input[n][kernel_H / 2][kernel_W / 2][l];\n",
    "\n",
    "    if (z00 < min_disp || z00 > max_disp)\n",
    "    {\n",
    "        output[n][0][l] = best_dx;\n",
    "        output[n][1][l] = best_dy;\n",
    "        return;\n",
    "    }\n",
    "\n",
    "    for (int i = 0; i < I; ++i)\n",
    "    {\n",
    "        int ids0 = random[n][l][i][0];\n",
    "        ids0 = (ids0 >= kernel_H * kernel_W / 2) ? ids0 + 1 : ids0;\n",
    "        int x0 = ids0 % kernel_W;\n",
    "        int y0 = ids0 / kernel_W;\n",
    "        float z0 = input[n][y0][x0][l];\n",
    "        if (z0 < min_disp || z0 > max_disp)\n",
    "        {\n",
    "            continue;\n",
    "        }\n",
    "        int ids1 = random[n][l][i][1];\n",
    "        ids1 = (ids1 >= kernel_H * kernel_W / 2) ? ids1 + 1 : ids1;\n",
    "        int x1 = ids1 % kernel_W;\n",
    "        int y1 = ids1 / kernel_W;\n",
    "        float z1 = input[n][y1][x1][l];\n",
    "        if (z1 < min_disp || z1 > max_disp)\n",
    "        {\n",
    "            continue;\n",
    "        }\n",
    "\n",
    "        // NOTE: CCJ's note:\n",
    "        // Sample 3 points P1, P2, and Pc to form a plane, and calculate the normal of the plane;\n",
    "        // where P1 = (x1,y1,z1), P2 = (x2,y2,z2) and Pc =(xc, yc,zc), here xc=kernel_W//2, yc=kernel_H//2, (i.e., \n",
    "        // the center of the kernel widnow).\n",
    "        // Given the vector vec(Pc,P1)=P1-Pc=(x1-xc, y1-yc,z1-zc) and the vector vec(Pc,P2)=P2-Pc=(x2-xc, y2-yc,z2-zc),\n",
    "        // we can get the normal vector via `cross-product` between vec(Pc,P1) and vec(Pc,P2),\n",
    "        // i.e., n=vec(Pc,P1) x vec(Pc,P1)\n",
    "        //               i         j          k\n",
    "        // vec(P1-Pc)  a=x1-xc  b=y1-yc  c=z1-zc\n",
    "        // vec(P2-Pc)  d=x2-xc  e=y2-yc  f=z2-zc\n",
    "        // Therefore, the normal n = i*(bf-ce) - j*(af-cd) + k*(ae-bd)\n",
    "        // i.e., \n",
    "        // vector n = (bf-ce, cd-af, ae-bd) ... (1)\n",
    "        // Given the normal and one point on the plane, say Pc, \n",
    "        // we can get the equation of the plane: Ax + By + Cz + D = 0\n",
    "        // where the normal     n = (A,B,C) ... (2)\n",
    "        // the derivative     dz/dx = - A/C ... (3)\n",
    "        // and the derivative dz/dy = - B/C ... (3)\n",
    "        // Submitting Eq. 1 to Eq. 2 ,3, and 4, we get\n",
    "        //   1) dz/dx = -(bf-ce)/(ae-bd) = (ce-bf)/(ae-bd), and  \n",
    "        //   2) dz/dY = -(cd-af)/(ae-bd) = (af-cd)/(ae-bd);\n",
    "        //   Done!\n",
    "        x0 -= (kernel_W / 2);\n",
    "        y0 -= (kernel_H / 2);\n",
    "\n",
    "        x1 -= (kernel_W / 2);\n",
    "        y1 -= (kernel_H / 2);\n",
    "\n",
    "        float c0 = z0 - z00;\n",
    "        float c1 = z1 - z00;\n",
    "\n",
    "        float dx = (c0 * y1 - y0 * c1) / (x0 * y1 - y0 * x1);\n",
    "        float dy = (x0 * c1 - c0 * x1) / (x0 * y1 - y0 * x1);\n",
    "\n",
    "        int inlier = 0;\n",
    "        for (int h = 0; h < kernel_H; ++h)\n",
    "        {\n",
    "            for (int w = 0; w < kernel_W; ++w)\n",
    "            {\n",
    "                float zwh = input[n][h][w][l];\n",
    "                if (zwh < min_disp || zwh > max_disp){\n",
    "                    continue;\n",
    "                }\n",
    "\n",
    "                float err = dx * (w - kernel_W / 2) + dy * (h - kernel_H / 2) - zwh + z00;\n",
    "                if (err < 0){\n",
    "                    err = -err;\n",
    "                }\n",
    "\n",
    "                if (err < sigma){\n",
    "                    ++inlier;\n",
    "                }\n",
    "            }\n",
    "        }\n",
    "\n",
    "        if (inlier > max_inlier)\n",
    "        {\n",
    "            max_inlier = inlier;\n",
    "            best_dx = dx;\n",
    "            best_dy = dy;\n",
    "        }\n",
    "    }\n",
    "\n",
    "    output[n][0][l] = best_dx;\n",
    "    output[n][1][l] = best_dy;\n",
    "    return;\n",
    "}\n",
    "\n",
    "/* CCJ's note: \n",
    " * Input is a disparity map D in shape of [N,C,H,W]=[N,1,H,W]\n",
    " * To fit a plane to disparity map D in a k x k= 9x9 window centered\n",
    " * at the pixel in D.\n",
    " * So here the input is actually a unfolded version of the original\n",
    " * disparity map D, e.g., `input` = torch.unfold(D, kernel_size=k, padding=k//2, stride=1);\n",
    " * resulting the `input` here in shape of [N*C, k, k, L]=[N,k,k,H*W] (with C=1);\n",
    " */\n",
    "torch::Tensor plane_fitting_cuda_foward(\n",
    "    torch::Tensor input, // [N*C, k, k, L]=[N,k,k,H*W] \n",
    "    int iter, \n",
    "    float sigma, \n",
    "    float min_disp, \n",
    "    float max_disp\n",
    "    ){\n",
    "    const auto N = input.size(0); // batch dimension\n",
    "    const auto kernel_H = input.size(1); // kernel height\n",
    "    const auto kernel_W = input.size(2); // kernel width\n",
    "    const auto L = input.size(3); // number of pixels\n",
    "\n",
    "    torch::Tensor random = torch::randint(\n",
    "        0, kernel_H * kernel_W - 1, \n",
    "        {N, L, iter, 2}, \n",
    "        torch::dtype(torch::kInt32).device(input.device())\n",
    "        );\n",
    "    torch::Tensor output = torch::ones(\n",
    "        {N, 2, L}, \n",
    "        torch::dtype(torch::kFloat32).device(input.device())\n",
    "        );\n",
    "\n",
    "    const at::cuda::OptionalCUDAGuard guard(device_of(input));\n",
    "    plane_fitting_cuda_foward_kernel<<<(N * L + 1023) / 1024, 1024>>>(\n",
    "        sigma,\n",
    "        min_disp,\n",
    "        max_disp,\n",
    "        input.packed_accessor32<float, 4, torch::RestrictPtrTraits>(),\n",
    "        random.packed_accessor32<int, 4, torch::RestrictPtrTraits>(),\n",
    "        output.packed_accessor32<float, 3, torch::RestrictPtrTraits>());\n",
    "    return output;\n",
    "}\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f105ee9c-3233-4ea4-b635-c5a0c78942aa",
   "metadata": {},
   "source": [
    "## Call the cuda library in Python"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9773b2dc-84ce-4a40-aabe-03f74d16f085",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 3 (ipykernel)",
   "language": "python",
   "name": "python3"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.9.13"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
